        SUBROUTINE MILEST
C
C   SUBROUTINE MILEST
C   SUBROUTINE MILEST CALCULATES MILESTONES OF STANDARD TRIGA MARK II CORE
C   CONFIGURATION. MILESTONE IS A DIAMETRAL LINE, PASSING THROUGH THE ORIGIN
C   (R=0) AND RIGHT EDGE (OBSERVING FROM TE CENTRE) OF AN ARBITRARY UNIT 
C   CELL. SEVERAL UNIT CELLS MAY SHARE THE SAME MILESTONE LINE.THE PURPOSE   
C   OF MILEST IS TO PREPARE THE THETA COORDINATES OF THE MILESTONES (THMIL) 
C   AND TO  ADRESS THEM TO ALL UNIT CELLS. 
C   ASSUMPTIONS:
C     1. ANULAR CORE, ELEMENTS OF THE SAME RING EQUIDISTANT IN THETA
C     2. STANDARD TRIGA MARK II GEOMETRY AND NOTATION
C     4. NUMBER OF UNIT CELLS (POSITIONS) IN RINGS A,B,C,D,E,F,G 
C        (RESPECTIVELY):
C        1,6,12,18,24,30,36.
C     5. UNIT CELL IS DEFINED AS A SECTION OF A ANNULUS (RING) LIMITED BY TWO 
C        MILESTONE LINES, EXCEPT CELL IN A,WHICH IS A CIRCLE. BEYOND 
C        G-RING REACTOR IS SUPPOSED TO CONSIST OF HOMOGENEOUS RINGS. 
C     6. THETA IS ASIMUTAL ANGLE MEASURED IN CLOCKWISE DIRECTION IN 
C        RADIANS. THETA=0 IS AT THE SYMMETRY LINE PASSING THROUGH 
C        THE UNIT CELLS AT A1, B1, C1, D1...
C        NOTE: STANDARD TRIGA GEOMETRY IS BUILT INTO THE CODE.IF GEOMETRY 
C        OR DEFFINITON OF FUEL ELEMENT LOCATIONS IS DIFFERENT FROM ABOVE
C        ASSUMPTIONS, MILEST IS NOT ADEQUATE. THIS IS, HOWEVER, NOT LIKELY.
C
C   INPUT: NO
C   OUTPUT: THMIL(102)
C           INMIL(7,36)=INDEX OF THE MILESTONE LINE (RUNS FROM 1 TO 102)
C                       WHICH IS THE RIGHT BORDER OF THE UNIT CELL AT 
C                       POSITION (RING,POSITION NUMBER). INMIL FOLLOWS:
C
C    A   NOT DEFINED
C    B   9 26 43 60 77 94
C    C   5 13 22 30 39 47 56 64 73 81 90 98
C    D   4  9 14 21 26 31 38 43 48 55 60 65 72 77 82 89 94 99
C    E   3  7 11 15 20 24 28 32 37 41 45 49 54 58 62 66 71 75
C       79 83 88 92 96 100
C    F   2  6  9 12 16 19 23 26 29 33 36 40 43 46 50 53 57 60
C       63 67 70 74 77 80 84 87 91 94 97 101 
C    G   1  5  8 10 13 17 18 22 25 27 30 34 35 39 42 44 47 51
C       52 56 59 61 64 68 69 73 76 78 81 85 86 90 93 95 98 102
C
C
      DIMENSION THMIL(102),INMIL(7,36)
      COMMON THMIL,INMIL
C
      PI=3.141592654            
      PIT=PI/3.
      THMIL(1)=PI/36.                                                      
      THMIL(2)=PI/30.
      THMIL(3)=PI/24.
      THMIL(4)=PI/18.
      THMIL(5)=PI/12.
      THMIL(6)=PI/10.
      THMIL(7)=PI/8.
      THMIL(8)=5*PI/36.
      THMIL(9)=PI/6.
C   SYMMETRY    
      THMIL(10)=PIT-THMIL(8)
      THMIL(11)=PIT-THMIL(7)
      THMIL(12)=PIT-THMIL(6)
      THMIL(13)=PIT-THMIL(5)
      THMIL(14)=PIT-THMIL(4)
      THMIL(15)=PIT-THMIL(3)
      THMIL(16)=PIT-THMIL(2)
      THMIL(17)=PIT-THMIL(1)
C   MORE SYMMETRY
      DO  1  J=1,17
      THMIL(17+J)=PIT+THMIL(J)
      THMIL(34+J)=2.*PIT+THMIL(J)
      THMIL(51+J)=3.*PIT+THMIL(J)
      THMIL(68+J)=4.*PIT+THMIL(J)
      THMIL(85+J)=5.*PIT+THMIL(J)
 1    CONTINUE
C   INMIL
      DO  2  J=1,6
      JM=(J-1)*17
C   B RING
      INMIL(2,J)=9+JM 
C   C RING
      INMIL(3,(2*J-1))= 5+JM
      INMIL(3,(2*J  ))=13+JM 
C   D RING
      INMIL(4,(3*J-2))= 4+JM
      INMIL(4,(3*J-1))= 9+JM
      INMIL(4,(3*J  ))=14+JM
C   E RING
      INMIL(5,(4*J-3))= 3+JM
      INMIL(5,(4*J-2))= 7+JM
      INMIL(5,(4*J-1))=11+JM
      INMIL(5,(4*J  ))=15+JM
C   F RING
      INMIL(6,(5*J-4))= 2+JM
      INMIL(6,(5*J-3))= 6+JM
      INMIL(6,(5*J-2))= 9+JM
      INMIL(6,(5*J-1))=12+JM
      INMIL(6,(5*J  ))=16+JM
C  G  RING
      INMIL(7,(6*J-5))= 1+JM
      INMIL(7,(6*J-4))= 5+JM
      INMIL(7,(6*J-3))= 8+JM
      INMIL(7,(6*J-2))=10+JM
      INMIL(7,(6*J-1))=13+JM
      INMIL(7,(6*J  ))=17+JM
 2    CONTINUE
      RETURN
      END
